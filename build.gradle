plugins {
	id 'org.cadixdev.licenser' version '0.6.1'
	id 'java-library'
	id 'maven-publish'
}

base {
	group = 'com.unascribed'
	archivesName = 'JXLJXL'
	version = '0.2.1'
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
	options.release = 20
	options.compilerArgs += ['--enable-preview']
}

sourceSets {
	viewer {
		compileClasspath += sourceSets.main.output
	}
}

license {
	header = project.file('LICENSE')
	
	exclude '**/com/unascribed/jxljxl/panama/**'
	exclude '**/org/imgscalr/**'
}

jar {
	from 'LICENSE'
}

task viewerJar(type: Jar) {
	from sourceSets.viewer.output
	
	archiveClassifier = 'viewer'
	
	manifest.attributes (
		'Main-Class': 'com.unascribed.jxljxl.viewer.JXLJXLViewer',
		'Class-Path': tasks.jar.archivePath.getName()
	)
}

tasks.build.dependsOn viewerJar

java {
	withSourcesJar()
}

publishing {
	repositories {
		if (project.hasProperty("publish-username")) {
			maven {
				url "https://repo-api.sleeping.town/"
				credentials {
					username project.hasProperty("publish-username") ? project.getProperty("publish-username") : null
					password project.hasProperty("publish-password") ? project.getProperty("publish-password") : null
				}
			}
		}
		maven {
			url file('build/maven').toURI().toString()
		}
	}
	publications {
		maven(MavenPublication) {
			groupId = 'com.unascribed'
			artifactId = 'jxljxl'
			version = project.version
			from components.java
		}
	}
}
